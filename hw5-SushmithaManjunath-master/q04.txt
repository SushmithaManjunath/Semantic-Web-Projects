# Find all universities in Maryland and their total number of known
# students as computed by summing their undergraduate and graduate
# students.  Assume 0 for an unknown number.  Sort the list from
# largest to smallest enrollment.


PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX yago: <http://dbpedia.org/class/yago/>

SELECT DISTINCT ?University, ?Undergraduate, ?Graduate, ( (if( bound(?Undergraduate), ?Undergraduate, 0)) + (if( bound(?Graduate), ?Graduate, 0)) as ?Total)
WHERE {
{ ?University a dbpo:University } UNION { ?University dbp:type dbr:Private_university } UNION { ?University dbp:type dbr:Public_university } .
?University dbpo:state dbr:Maryland . 
OPTIONAL { ?University dbpo:numberOfPostgraduateStudents  ?Graduate } .
OPTIONAL { ?University dbpo:numberOfUndergraduateStudents  ?Undergraduate  }
}
ORDER BY DESC(?Total)